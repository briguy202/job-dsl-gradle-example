apply plugin: 'groovy'

sourceSets {
    jobs {
        groovy {
            srcDirs 'jobs'
            compileClasspath += main.compileClasspath
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'http://repo.jenkins-ci.org/releases/' }
}

configurations {
    testPlugins {}
}

// Exclude buggy Xalan dependency this way the JRE default TransformerFactory is used
// The xalan pulled in by htmlunit does not properly deal with spaces folder / job names
configurations.all*.exclude group: 'xalan'

dependencies {
    compile files('/Users/brian/Development/amway-account-build/jenkins-jobs/build/libs/jenkins-jobs.jar')
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    compile "org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}"

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'cglib:cglib-nodep:2.2.2' // used by Spock

    // Jenkins test harness dependencies
    testCompile 'org.jenkins-ci.main:jenkins-test-harness:2.8'
    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"
    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}:war-for-test@jar"

    // Job DSL plugin including plugin dependencies
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testCompile 'org.jenkins-ci.plugins:structs:1.2@jar'
    testCompile 'org.jenkins-ci.plugins:cloudbees-folder:5.0@jar'

    // plugins to install in test instance
    testPlugins 'org.jenkins-ci.plugins:nodejs:0.2.2'
    testPlugins 'org.jenkins-ci.plugins:mask-passwords:2.9'
    testPlugins 'org.jenkins-ci.plugins:jenkins-multijob-plugin:1.23'
    testPlugins 'org.jenkins-ci.plugins:credentials:2.1.8'
    testPlugins 'org.jenkins-ci.plugins:credentials-binding:1.9'
    testPlugins 'org.jenkins-ci.plugins:envinject:1.93.1'
    testPlugins 'org.jenkins-ci.plugins:timestamper:1.8.7'
    testPlugins 'org.jenkins-ci.plugins:cobertura:1.9.8'
    testPlugins 'org.jenkins-ci.plugins:git:3.0.0'
    testPlugins 'org.jvnet.hudson.plugins:analysis-collector:1.49'
    testPlugins 'org.jenkins-ci.plugins:slack:2.0.1'
    testPlugins 'org.jenkins-ci.plugins:ghprb:1.33.1'
    testPlugins 'com.coravy.hudson.plugins.github:github:1.22.4'
    testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:5.0'

    // for the RestApiScriptRunner
    compile('org.codehaus.groovy.modules.http-builder:http-builder:0.7.2') {
        exclude(module: 'groovy')
    }

    // for the RestApiScriptRunner
    compile('org.apache.ant:ant:1.9.7')
}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'

    doLast {
        def baseNames = source.collect { it.name[0..it.name.lastIndexOf('.')-1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs

    exclude '**/XmlOutput.class'

    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
}

task rest(dependsOn: 'classes', type: JavaExec) {
    main = 'com.dslexample.rest.RestApiScriptRunner'
    classpath = sourceSets.main.runtimeClasspath
    systemProperties System.getProperties()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}
